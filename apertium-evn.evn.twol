Alphabet

 а б в г д е ё ж з и й к л м н ӈ о п р с т у ф х һ ц ч ш щ ъ ы ь э ю я
 А Б В Г Д Е Ё Ж З И Й К Л М Н Ӈ О П Р С Т У Ф Х Һ Ц Ч Ш Щ Ъ Ы Ь Э Ю Я

 %{ː%}:̄

 %{L%}:л %{L%}:р
 %{G%}:г %{G%}:к %{G%}:ӈ
 %{Г%}:г %{Г%}:к %{Г%}:ӈ %{Г%}:в
 %{D%}:д %{D%}:т %{D%}:ч
 %{A%}:а %{A%}:э %{A%}:о
 %{Я%}:а %{Я%}:э %{Я%}:о %{Я%}:я %{Я%}:е %{Я%}:ё 
 %{V%}:в %{V%}:м
 %{N%}:н %{N%}:т

 %{r%}:р %{r%}:т %{r%}:л %{r%}:н %{r%}:0
 %{i%}:0 %{i%}:и %{i%}:ы
 %{ы%}:0 %{ы%}:ы
 %{g%}:0 %{g%}:г %{g%}:к
 %{u%}:0 %{u%}:у
 %{ъ%}:0 %{ъ%}:ъ

 %{а%}:0 %{э%}:0 
 %{ч%}:0 %{д%}:0
 %{n%}:0 %{ь%}:0 %{☭%}:0

 %>:0  
 %-
;

Sets

Vow = а е ё и о у ы э ю я 
      А Е Ё И О У Ы Э Ю Я ;

VowNeutral = и ы у ю
             И Ы У Ю ;

VowA =  а я
        А Я ;

VowO = о ё
       О ё ;

VowE =  э е 
        Э Е ; 

Cons = б в г д ж з й к л м н ӈ п р с т ф х һ ц ч ш щ 
       Б В Г Д Ж З Й К Л М Н Ӈ П Р С Т Ф Х Һ Ц Ч Ш Щ ;

NonLabialCons = г д ж з й к л н ӈ р с т х һ ц ч ш щ 
          Г Д Ж З Й К Л Н Ӈ Р С Т Х Һ Ц Ч Ш Щ ;

Nasal = м н ӈ
        М Н Ӈ ; 

VoicelessCns = к с т п
               К С Т П ;

Rules 

"Remove ь before suffix vowel {i}"
ь:0 <=> _ :0* %{i%}: ;

!# автомобиль>{i}л
!# автомобил00ил
!@ а в т о м о б и л ь>0 >:0 {i}:и л

"Remove й before suffix vowel {i}"
й:0 <=> _ :0* %{i%}:и ; ! should %{i%} be with %{ː%} ?

!# учёнай>{i}л
!# учёна00ил
!@ у ч ё н а й:0 >:0 {i}:и л

"Remove suffix vowel {i}"
%{i%}:0 <=>    :Vow ( %{ː%}: ) :0* _ ;
             :Vow ( %{ː%}: ) :0* т _ к и ;
                             %>: ӈ _ (%{ː%}:) %>: Cons+ Vow+ ;
                %>: %{D%}: у к %>: _ Cons+ Vow+ ;
                               %>: _ [ в у н | т ы н ] ;
  [ н | %>: %{i%}: л | %>: р ] %>: _ т:д %{д%}: ;
                         %{u%}: >: _ ;
                          т %{д%}: _ ;
             except
                           й:0 :0* _ ;

!# мо{ː}>{i}л
!# мо̄00л
!@ м о {ː}:̄ >:0 {i}:0 л

!# мо{ː}>т{i}ки
!# мо̄00т0ки
!@ м о {ː}:̄ >:0 т {i}:0 к и

!# орон>{D}ук>{i}вун
!# орон0дук00вун
!@ о р о н >:0 {D}:д у к >:0 {i}:0 в у н

!# дю{а}>{G}ит{д}{i}>{i}вун
!# дю0гид0и00вун
!@ д ю {а}:0 >:0 {G}:г и т:д {д}:0 {i}:и >:0 {i}:0 в у н

"Suffix vowel {i} realised as ы after т or д"
%{i%}:ы <=> [ т | д ] :0* _ ;
        except 
                   %{д%}: _ ;
  [ %{ь%}: | %{ч%}: ] :0* _ ;
    :Vow ( %{ː%}: ) :0* т _ к и ;
                      %>: _ [ в у н | т ы н ] ;
                %{u%}: >: _ ;

!# дэт>{i}кли
!# дэт0ыкли
!@ д э т >:0 {i}:ы к л и

!# хуна{ː}т{д}>{i}л
!# хуна̄д00ил
!@ х у н а {ː}:̄ т:д {д}:0 >:0 {i}:и л

!# чаӈит{ь}>{i}л
!# чаӈит00ил
!@ ч а ӈ и т {ь}:0 >:0 {i}:и л

"Remove н before the sign {n}"
н:0 <=> _ %{n%}: ;

"Assimilate possessive to plural"
%{L%}:Cx <=> Cx %>: _ ;
       where Cx in ( л р ) ;

!# хутэ>{i}л>{L}и
!# хутэ00л0ли
!@ х у т э >:0 {i}:0 л >:0 {L}:л и

!# оро>р>{L}ун
!# оро0р0рун
!@ о р о >:0 р >:0 {L}:р у н

"Consonant assimilation in suffixes"
%{G%}:Cy <=> :Cx :0* (%-) _ ;
       where Cx in ( к с т п м н ӈ ) 
             Cy in ( к к к к ӈ ӈ ӈ ) 
       matched ; 

!# дэт>{G}ит
!# дэт0кит	
!@ д э т >:0 {G}:к и т

!# курим>{G}ит
!# курим0ӈит
!@ к у р и м >:0 {G}:ӈ и т

"Consonant assimilation in suffixes"
%{Г%}:Cy <=> :Cx :0* _ ;
       where Cx in ( к с т п м н ӈ г ) 
             Cy in ( к к к к ӈ ӈ ӈ в ) 
       matched ; 

"Voicing assimilation of {D}"
%{D%}:т <=> [ :VoicelessCns | :Cons :0* %{☭%}: ] :0* ( %- ) _ ;
                                       except
                                                             _ %{Я%}: ;
                                   :Vow ( %{ː%}: | :0* ) %>: _ %{u%}: ;
                              :Nasal :0* %{☭%}:  :0* ( %- ) _ ;

!# тар>-{D}{A}{ː}
!# тар0-да̄
!@ т а р >:0 - {D}:д {A}:а {ː}:̄

!# дэт>{D}{u}ли	
!# дэт0тули
!@ д э т >:0 {D}:т {u}:у л и

"{D} realises as ч"
%{D%}:ч <=> [ :VoicelessCns | %{☭%}: ] :0* %>: _ %{Я%}: ;
         except
                 :Nasal :0* %{☭%}:  :0* ( %- ) _ ;

!# эе{ː}{э}т>{D}{Я}>м
!# эе̄0т0чэ0м
!@ э е {ː}:̄ {э}:0 т >:0 {D}:ч {Я}:э >:0 м

"Remove {D} after vowel-ending stem when used in <locall> and <prl> cases"
%{D%}:0 <=> :Vow ( %{ː%}: | :0* ) %>: _ %{u%}: ;

!# голо>{D}{u}ли
!# голо000ли
!@ г о л о >:0 {D}:0 {u}:0 л и

"Remove {u} after vowel-ending stem when used in <locall> and <prl> cases and after {V}:в before consonant"
%{u%}:0 <=> %>: %{D%}:0 _ ;
              ! Passive
                %>: %{V%}:в _ %>: :NonLabialCons ;
              ! Causative
                %>: %{V%}: _ к %{A%}: н ;
         except
                :Nasal %>: %{V%}: _ к %{A%}: н ;

!# тэты>{V}{u}>{i}м
!# тэты0ву0{i}м
!@ т э т ы >:0 {V}:в {u}:у >:0 {i}:0 м

!# голо>{D}{u}ли
!# голо000ли
!@ г о л о >:0 {D}:0 {u}:0 л и

!# тэты>{V}{u}>{r}{A}>н
!# тэты0в00ра0н
!@ т э т ы >:0 {V}:в {u}:0 >:0 {r}:р {A}:э >:0 н

! Two Passive Markers
!# о{ː}>{V}{u}>{V}{u}>ч{A}{ː}
!# о̄0ву0в00чā
!@ о {ː}:̄ >:0 {V}:в {u}:у >:0 {V}:в >:0 ч {A}:а {ː}:̄ 

! Causative
!# о{ː}>{V}{u}к{A}н>д{Я}>ри{ː}
!# ō0в0кāн0дя0рӣ
!@ о {ː}:̄ >:0 {V}:в к {A}:а {ː}:̄ н >:0 д {Я}:я >:0 р и {ː}:̄ 

"Voicing assimilation of {g}"
%{g%}:к <=> :VoicelessCns %>: _ ;

"Remove {g} after Vow"
%{g%}:0 <=> :Vow %>: _ ; 

"{D}:д after Nasal in <comp> and <sup> suffixes"
%{D%}:д <= :Nasal :0* (%-) _ %{ы%}: ;

"ы appears after {D}:д in <comp> and <sup> suffixes"
%{ы%}:ы <= %{D%}:д _ ; 

!# баян>{D}{ы}м{A}р	
!# баян0дымар
!@ б а я н >:0 {D}:д {ы}:ы м {A}:а р

"т turns into ч"
т:ч <=> _ %{ч%}: %>: :и ;

!# таткит{ч}>{i}л	
!# таткич00ил
!@ т а т к и т:ч {ч}:0 >:0 {i}:и л

"т turns into д"
т:д <=>  [ н | %>: %{i%}: л | %>: р ] >: %{i%}: _ %{д%}: ;
                                                _ %{д%}: ( %{i%}: ) %>: %{i%}: ;

!# орон>{G}ит{д}{i}>{i}вун
!# орон0ӈид0и00вун
!@ о р о н >:0 {G}:ӈ и т:д {д}:0 {i}:и >:0 {i}:0 в у н

"Nasal assimilation of {V}" 
%{V%}:м <=> :Nasal :0* (%-) _ ; 

!# орон>{V}{A}>{i}в
!# орон0мо00в
!@ о р о н >:0 {V}:м {A}:о >:0 {i}:0 в

"Assimilation of {r}"
%{r%}:Cy <=> :Cx :0* _ ;
         where Cx in ( н к с т п л м ӈ )
               Cy in ( 0 т т т т л н н )
         matched ;

!# тэт>{r}{A}
!# тэт0тэ
!@ т э т >:0 {r}:т {A}:э

!# у{а}м>{r}{A}
!# у0м0на
!@ у {а}:0 м >:0 {r}:н {A}:а

"Assimilation of {N} before suffixes starting with т, ч"
%{N%}:т <=> _ %>: [ т | ч ] ; 

"Vowel harmony for {A}: a is chosen"
%{A%}:а <=>   :VowA %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  _ %{ː%}: ;
              :VowO %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  _ ;
                                                      :VowO ?* [ :Vow - :VowO ] ?* _ ;
                                                                   .#. :э %{ː%}: ?* _ ;
                                                            .#. :Cons+ :е %{ː%}: ?* _ ;
                      :VowA [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* _ ;
                                                                         %{а%}: ?*  _ ;
              except
                                                                        %{э%}: ?*  _ ;
                                              :VowO [ :Cons | :0 | :ь | :ъ | %- ]* _ ;

"Vowel harmony for {A}: э is chosen"
%{A%}:э <=>  [ :VowE | :VowA ] %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  _ ;                                                     
                                 :VowE [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* _ ;
                                                                                   %{э%}: ?*  _ ;
             except
                        :VowA %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  _ %{ː%}: ;
                                                                             .#. :э %{ː%}: ?* _ ;
                                                                      .#. :Cons+ :е %{ː%}: ?* _ ;
                                                                        [ :VowO | %{а%}: ] ?* _ ;


"Vowel harmony for {A}: о is chosen"
%{A%}:о <=> :VowO [ :Cons | :0 | :ь | :ъ | %- ]* _ ;
       except
!             [ :о | :ё ] ?* [ :Vow - :VowO ] ?*  _ ;
                         [ %{а%}: | %{э%}: ] ?*  _ ;


"Non-yotised vowel harmony for {Я}: a is chosen"
%{Я%}:а <=>     :VowA %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ %{ː%}: ;
                                   :VowA %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* [ %{D%}:ч | %{D%}:т ] _ %{ː%}: ;
                                                       [  %{а%}: | :VowO %{ː%}: ] ?* [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ ;
                                                                         [  %{а%}: | :VowO %{ː%}: ] ?*  [ %{D%}:ч | %{D%}:т ] _ ;
                                                       :VowO ?* [ :Vow - :VowO ] ?*  [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ ;
                                                                           :VowO ?* [ :Vow - :VowO ] ?* [ %{D%}:ч | %{D%}:т ] _ ;
                                                                    .#. :э %{ː%}: ?* [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ ;
                                                                                      .#. :э %{ː%}: ?*  [ %{D%}:ч | %{D%}:т ] _ ;
                                                              .#. Cons+ :е %{ː%}: ?* [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ ;
                                                                                 .#. Cons+ :е %{ː%}: ?* [ %{D%}:ч | %{D%}:т ] _ ;
                       :VowA [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ ;
                                          :VowA [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* [ %{D%}:ч | %{D%}:т ] _ ;
          except
                                                                                                                   %{э%}: ?*  _ ;
                                                                                         :VowO [ :Cons | :0 | :ь | :ъ | %- ]* _ ;


"Non-yotised vowel harmony for {Я}: э is chosen"
%{Я%}:э <=> [ :VowE | :VowA ] %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0  _ ;
                              [ :VowE | :VowA ] %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  [ %{D%}:ч | %{D%}:т ]  _ ;
                                                                                   %{э%}: ?* [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0  _ ;
                                                                                                     %{э%}: ?*  [ %{D%}:ч | %{D%}:т ]  _ ;
                                                   :VowE [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* [ %{D%}:ч | %{D%}:т ] _ ;
                                :VowE [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ ;
           except
                                                                :VowA %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  _ %{ː%}: ;
                                                                                                                     .#. :э %{ː%}: ?* _ ;
                                                                                                              .#. :Cons+ :е %{ː%}: ?* _ ;
                                                                                                               [ :VowO | %{а%}: ] ?* _ ;
"Non-yotised vowel harmony for {Я}: о is chosen"
%{Я%}:о <=>  :VowO [ :Cons | :0 | :ь | :ъ | %- ]* [ %>: %{i%}:0 л | н | %>: р ] %>: %{ъ%}:0 _ ;
                               :VowO [ :Cons | :0 | :ь | :ъ | %- ]*  [ %{D%}:ч | %{D%}:т ] _ ;
          except
!                                                      [ :о | :ё ] ?* [ :Vow - :VowO ] ?*  _ ;
                                                                   [ %{а%}: | %{э%}: ] ?*  _ ;


"Yotised vowel harmony for {Я}: я is chosen"
%{Я%}:я <=>   :VowA %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  _ %{ː%}: ;
              :VowO %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  _ ;
                                                      :VowO ?* [ :Vow - :VowO ] ?* _ ;
                                                                  .#. :э %{ː%}: ?* _ ;
                                                           .#. :Cons+ :е %{ː%}: ?* _ ;
                                                                        %{а%}: ?*  _ ;
                     :VowA [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* _ ;

          except
                                                                         %{э%}: ?* _ ;
                                          [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ ;
                                                             [ %{D%}:ч | %{D%}:т ] _ ;
                                              :VowO [ :Cons | :0 | :ь | :ъ | %- ]* _ ;

"Yotised vowel harmony for {Я}: е is chosen"
%{Я%}:е <=>  [ :VowE | :VowA ] %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  _ ;
                                 :VowE [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]* _ ;
                                                                                    %{э%}: ?*  _ ;
         except
                          :VowA %{ː%}: [ :Cons | :VowNeutral ( %{ː%}: ) | :0 | :ь | :ъ | %- ]*  _ %{ː%}: ;
                                                      [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ ;
                                                                         [ %{D%}:ч | %{D%}:т ] _ ;
                                                                              .#. :э %{ː%}: ?* _ ;
                                                                       .#. :Cons+ :е %{ː%}: ?* _ ;
                                                                         [ :VowO | %{а%}: ] ?* _ ;


"Yotised vowel harmony for {Я}: ё is chosen"
%{Я%}:ё <=> :VowO [ :Cons | :0 | :ь | :ъ | %- ]* _ ;
    except 
!             [ :о | :ё ] ?* [ :Vow - :VowO ] ?*  _ ;
                         [ %{а%}: | %{э%}: ] ?*  _ ;
        [ %>: %{i%}: л | н | %>: р ] %>: %{ъ%}:0 _ ;
                           [ %{D%}:ч | %{D%}:т ] _ ;

"ъ appears after Cons"
%{ъ%}:ъ <=>                   :Cons :0* %>: _ ;
              except
           [ %>: %{i%}: л | н | %>: р ] %>: _ ;

!# уму{а}к>{ъ}{Я}
!# уму0к0ъя
!@ у м у {а}:0 к >:0 {ъ}:ъ {Я}:я

!# дэт>{ъ}{Я}	
!# дэт0ъе
!@ д э т >:0 {ъ}:ъ {Я}:е

!cat apertium-evn.evn.twol | grep '^!@' | cut -f2 -d'@' | hfst-pair-test .deps/evn.twol.hfst
